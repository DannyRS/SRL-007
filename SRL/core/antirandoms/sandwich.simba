(*
Sandwich Lady
=============

Stores all the routines to solve the Sandwich Lady random.  None of these
routines should be used throughout scripts.  They only need to be called in
SRL's random detection methods.

This solver uses text recognition to determine which food to click,
then uses TPA Scanning to find and click the required food.

*)

const
  _SL_Debug = False;

var
  _SL_TimeOut: Integer;

function SL_StopSearch: boolean;
begin
  Result := False;

  if (CountColor(39423, 490, 10, 513, 32) < 5) then
    Result := True;

  if ((GetSystemTime - _SL_TimeOut) > 35000) then
    Result := True;
end;

procedure SL_Message(Message: string);
begin
  addToSRLLog('[Sandwich Random] ' + Message);
end;

(**
 * Author: DannyRS
 * Last modified 15/03/2013 By DannyRS
 * Description: Selects a sandwich number 0-6 (left-right)
 *)
function SL_Select(SandwichNumber: integer): boolean;
var
  SPoint: tpoint;
begin
  Result := False;
  SPoint := Point(-1,-1);

  case SandwichNumber of
    0: SPoint := Point(155, 120);
    1: SPoint := Point(260, 122);
    2: SPoint := Point(365, 132);
    3: SPoint := Point(110, 210);
    4: SPoint := Point(205, 219);
    5: SPoint := Point(315, 213);
    6: SPoint := Point(395, 217);
  end;

  if (SPoint.X < 0) then
  begin
    SL_Message('[ERROR] Invalid sandwich number');
    Exit;
  end;

  if (SL_StopSearch) then Exit;

  MMouse(RandomRange(SPoint.X-4,SPoint.X+4),
      RandomRange(SPoint.Y-4,SPoint.Y+4), 0, 0);

  ClickMouse2(mouse_left);

  Result := True;
end;

(**
 * Author: DannyRS
 * Last modified 15/03/2013 By DannyRS
 * Description: Gets sandwich name from screen
 *)
function SL_GetSandwichName: string;
var
  FoundString, SWName: string;
begin
  Result := ''; SWName := '';

  FoundString := Trim(GetTextAtExWrap(80, 270, 400, 303,
      0, 3, 1, 2070783, 0, 'UpChars07'));

  SWName := Trim(Between('Have a', 'for free', FoundString));

  if (not (SWName='')) then
    Result := SWName;
end;

(**
 * Author: DannyRS
 * Last modified 15/03/2013 By DannyRS
 * Description: Returns TBox area of all the sandwiches
 *)
function SL_SandwichSearchBox(BoxNumber: integer): tbox;
begin
  case BoxNumber of
    0: Result := IntToBox(100,80,205,155);
    1: Result := IntToBox(215,80,305,160);
    2: Result := IntToBox(320,80,410,163);
    3: Result := IntToBox(71,170,160,255);
    4: Result := IntToBox(165,175,255,260);
    5: Result := IntToBox(260,175,355,265);
    6: Result := IntToBox(360,175,435,270);
  end;
end;

(**
 * Author: DannyRS
 * Last modified 21/05/2013 By DannyRS
 * Description: Finds square and triangle sandwiches
 *)
function SL_FindSandwichBySize(PickLargest: Boolean): Integer;
var
  SB: TBox;
  SWTPA: TPointArray;
  SBoxes, CNums: TIntegerArray;
  i, j, t: Integer;
begin

  SBoxes := [-1, -1]
  CNums  := [-1, -1]
  j := 0;

  SetColorToleranceSpeed(2);
  SetToleranceSpeed2Modifiers(0.02, 0.21);

  repeat

    for i:=0 to 6 do
    begin
      SB := SL_SandwichSearchBox(i);
      FindColorsTolerance(SWTPA, 1345203, SB.X1, SB.Y1, SB.X2, SB.Y2, 13);

      if (Length(SWTPA) < 10) or (j > 1) then
        Continue;

      if (_SL_Debug) then
        SL_Message('[Size Locator] Possible sandwich located at ' + IntToStr(i) +
                   ': ' + IntToStr(Length(SWTPA)));

      SBoxes[j] := i;

      j := j + 1;
    end;

  until ((SBoxes[0] > -1) and (SBoxes[1] > -1)) or (SL_StopSearch);

  SetColorToleranceSpeed(2);
  SetToleranceSpeed2Modifiers(0.03, 0.33);

  t := GetSystemTime;

  repeat

    SB := SL_SandwichSearchBox(SBoxes[0]);
    FindColorsTolerance(SWTPA, 5078928, SB.X1, SB.Y1, SB.X2, SB.Y2, 13);

    if (CNums[0] < Length(SWTPA)) then
      CNums[0] := Length(SWTPA);

    SB := SL_SandwichSearchBox(SBoxes[1]);
    FindColorsTolerance(SWTPA, 5078928, SB.X1, SB.Y1, SB.X2, SB.Y2, 13);

    if (CNums[1] < Length(SWTPA)) then
      CNums[1] := Length(SWTPA);

    if (_SL_Debug) then
        SL_Message('[Size Locator] Sizes : ' +
                    IntToStr(CNums[0]) + ' : ' + IntToStr(CNums[1]));

    Wait(300);

  until ((GetSystemTime - t) > 5000) or (SL_StopSearch);

  if (SL_StopSearch) then
    Exit;

  if (PickLargest) then
  begin
    if (CNums[0] > CNums[1]) then
      Result := SBoxes[0]
    else
      Result := SBoxes[1]
  end
  else
  begin
    if (CNums[0] > CNums[1]) then
      Result := SBoxes[1]
    else
      Result := SBoxes[0]
  end;

end;

(**
 * Author: DannyRS
 * Last modified 15/03/2013 By DannyRS
 * Description: Finds the correct box number to click
 *)
function SL_FindSandwichBoxNumber: Integer;
var
  tmpCTS, i, SColor, STol, MinSSize: Integer;
  tmpHM, tmpSM, SHM, SSM: Extended;
  SName: String;
  SB: TBox;
  SWTPA: TPointArray;
begin
  Result := -1;

  SName := SL_GetSandwichName;

  tmpCTS := GetColorToleranceSpeed;
  GetToleranceSpeed2Modifiers(tmpHM, tmpSM);

  if (SName='') then
  begin
    SL_Message('[ERROR] Unable to get sandwich name');
    Exit;
  end;

  SL_Message('Sandwich Name: ' + SName);

  case Lowercase(SName) of
    'baguette'         : begin
                           SColor:=2712694;
                           STol:=11;
                           SHM:=0.03;
                           SSM:=0.19;
                           MinSSize:=30;
                         end;
    'chocolate bar'    : begin
                           SColor:=2111836;
                           STol:=10;
                           SHM:=0.05;
                           SSM:=0.22;
                           MinSSize:=75;
                         end;
    'kebab'            : begin
                           SColor:=609881;
                           STol:=2;
                           SHM:=0.06;
                           SSM:=0.71;
                           MinSSize:=15;
                         end;
    'pie'              : begin
                           SColor:=5395289;
                           STol:=24;
                           SHM:=0.10;
                           SSM:=0.08;
                           MinSSize:=70;
                         end;
    'bread roll'       : begin
                           SColor:=1280167;
                           STol:=9;
                           SHM:=0.02;
                           SSM:=0.24;
                           MinSSize:=75;
                         end;
    'triangle sandwich': begin
                           Result := SL_FindSandwichBySize(False);
                           SetColorToleranceSpeed(tmpCTS);
                           SetToleranceSpeed2Modifiers(tmpHM, tmpSM);
                           Exit;
                         end;
    'square sandwich'  : begin
                           Result := SL_FindSandwichBySize(True);
                           SetColorToleranceSpeed(tmpCTS);
                           SetToleranceSpeed2Modifiers(tmpHM, tmpSM);
                           Exit;
                         end;
    else
    begin SL_Message('[ERROR] Invalid sandwich name found'); Exit; end;
  end;

  SL_Message('Scanning for sandwich');
  SetColorToleranceSpeed(2);
  SetToleranceSpeed2Modifiers(SHM, SSM);

  repeat

    for i:=0 to 6 do
    begin
      SB := SL_SandwichSearchBox(i);
      FindColorsTolerance(SWTPA, SColor, SB.X1, SB.Y1, SB.X2, SB.Y2, STol);

      if (Length(SWTPA) < MinSSize) then
        Continue;

      if (_SL_Debug) then
        SL_Message('Sandwich located at ' + IntToStr(i) +
                   ': ' + IntToStr(Length(SWTPA)));
      Result := i;
      Break;
    end;

  until (Result > -1) or (SL_StopSearch);

  SetColorToleranceSpeed(tmpCTS);
  SetToleranceSpeed2Modifiers(tmpHM, tmpSM);
end;

(**
 * Author: DannyRS
 * Last modified 15/03/2013 By DannyRS
 * Description: Solves the Sandwich Lady random event
 *)
function SL_Solve: boolean;
begin
  Result := False;

  _SL_TimeOut := GetSystemTime;

  Wait(RandomRange(600, 800));

  ClickContinue(False);

  Wait(RandomRange(1300,1800));

  if (SL_StopSearch) then
  begin
    SL_Message('[ERROR] SandwichLady select screen not up');
    Exit;
  end;

  //if (_SL_Debug) then
    //takeScreen('Sandwich Random ' + SL_GetSandwichName);

  repeat

    if SL_Select(SL_FindSandwichBoxNumber) then
    begin
      Result := True;
      SL_Message('Selected sandwich')
    end;

    Wait(RandomRange(1100, 1800));

  until (CountColor(39423, 490, 10, 513, 32) < 5) or (SL_StopSearch);

  if ((GetSystemTime - _SL_TimeOut) > 35000) then
    SL_Message('[ERROR] SandwichLady solver timed out')
  else
    SL_Message('[SOLVED]');

  ClickContinue(False);

  Wait(RandomRange(1600, 1900));

end;
